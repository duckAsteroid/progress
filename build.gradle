buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
description = 'Java progress reporting library'

subprojects {
    /* Java basics */
    apply plugin: 'java'
    group = 'org.duck.asteroid'
    version = '1.0.0'
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile 'junit:junit:4.+'
    }

    /* Add ability to publish JARs to Maven (e.g. bintray) */
    apply plugin: 'maven-publish'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    /* Configuration to publish from travis to bintray */
    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['mavenJava'] // reference above publication
        publish = true // Whether version should be auto published after an upload
        override = true // Whether to override version artifacts already published
        pkg {
            name = task.project.name
            repo = 'maven'
            userOrg = 'duck-asteroid'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/duckAsteroid/progress.git'
        }
    }
}

task bintrayUploadAll {
    group 'publishing'
    description 'Call :bintrayUpload on all subprojects'
    subprojects.each {
        if (it.plugins.withType(com.jfrog.bintray.gradle.BintrayPlugin)) {
            dependsOn("${it.path}:bintrayUpload")
        }
    }
}
