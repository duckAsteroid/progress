plugins {
    id 'com.jfrog.bintray' version '1.8.5' apply(false)
    id 'org.sonarqube' version '2.8' apply(false)
    id 'jacoco'
    id "com.github.ben-manes.versions" version "0.28.0"
}
description = 'Java progress reporting library'

allprojects {
    group = 'com.asteroid.duck.progress'
    version = '1.1.0'
}

subprojects {
    /* Java basics */
    apply plugin: 'java'
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile 'junit:junit:4.+'
    }

    /* Test coverage */
    apply plugin: 'jacoco'
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    /* sonarqube cloud */
    apply plugin: 'org.sonarqube'
    sonarqube {
        properties {
            property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        }
    }

    /* Add ability to publish JARs to Maven (e.g. bintray) */
    apply plugin: 'maven-publish'
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                group = project.group
                version = project.version
            }
        }
    }

    /* Configuration to publish from travis to bintray */
    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        pkg {
            repo = 'maven'
            userOrg = 'duck-asteroid'
            name = project.name
            licenses = ['MIT']
            vcsUrl = 'https://github.com/duckAsteroid/progress.git'
        }
        //dryRun = true // used to test
        publish = true // Whether version should be auto published after an upload
        override = true // Whether to override version artifacts already published
        publications = ['maven']
    }
}


