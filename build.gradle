plugins {
    id 'com.jfrog.bintray' version '1.8.4'
    id "org.sonarqube" version "2.7.1"
    id "jacoco"
}
description = 'Java progress reporting library'

allprojects {
    group = 'com.asteroid.duck.progress'
    version = '1.0.3'
}

subprojects {
    /* Java basics */
    apply plugin: 'java'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile 'junit:junit:4.+'
    }

    /* Add ability to publish JARs to Maven (e.g. bintray) */
    apply plugin: 'maven-publish'
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                group = project.group
                version = project.version
            }
        }
    }

    /* Configuration to publish from travis to bintray */
    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        pkg {
            repo = 'maven'
            userOrg = 'duck-asteroid'
            name = project.name
            licenses = ['MIT']
            vcsUrl = 'https://github.com/duckAsteroid/progress.git'
        }
        publish = true // Whether version should be auto published after an upload
        override = true // Whether to override version artifacts already published
        publications = ['maven']
    }
}


